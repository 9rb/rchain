// Rholang macro parameters:
// minimumBond - the minimum bond allowed by the PoS
// maximumBond - the maximum bond allowed by PoS
// initialBonds - the initial bonds map

/*
 The table below describes the required computations and their dependencies

 No. | Dependency | Computation method | Result
 ----+------------+--------------------+-----------------------------------------------------------------------------------------------------------------------------------------------------
 1.  |            | given              | sk = a9585a0687761139ab3587a4938fb5ab9fcba675c79fefba889859674046d4a5
 2.  |            | given              | timestamp = 1559156420651
 3.  |            | lastNonce          | nonce = 9223372036854775807
 4.  | 1,         | secp256k1          | pk = 047b43d6548b72813b89ac1b9f9ca67624a8b372feedd71d4e2da036384a3e1236812227e524e6f237cde5f80dbb921cac12e6500791e9a9ed1254a745a816fe1f
 5.  | 4, 2,      | genIds             | uname = Unforgeable(0xa32c1649f1700469f125ea85f9c2a220072e593ae617213782d738e389639d3b)
 6.  | 3, 5,      | registry           | value = (9223372036854775807, bundle+ {   Unforgeable(0xa32c1649f1700469f125ea85f9c2a220072e593ae617213782d738e389639d3b) })
 7.  | 6,         | protobuf           | toSign = 2a40aa013d0a0d2a0b10feffffffffffffffff010a2c5a2a0a263a240a220a20a32c1649f1700469f125ea85f9c2a220072e593ae617213782d738e389639d3b1001
 8.  | 7, 1,      | secp256k1          | sig = 3044022054ff4bae3984252b116e41e28d98bb5533eaa39aec2729228159166e2784f641022066a0fd99e7ea33df812fab095cbe61250f9548bce6da3ec4c6a90c741b94087f
 9.  | 4,         | registry           | uri = rho:id:m3xk7h8r54dtqtwsrnxqzhe81baswey66nzw6m533nyd45ptyoybqr
 ----+------------+--------------------+-----------------------------------------------------------------------------------------------------------------------------------------------------
 */
new  PoS,
    registryLookup(`rho:registry:lookup`),
    revAddressOps(`rho:rev:address`),
    revVaultCh, listOpsCh,
    getUser,
    getCurrentUserAddress,
    getCurrentUserVault,
    getMVar,
    runMVar,
    deposit, distributeRewards,
    rs(`rho:registry:insertSigned:secp256k1`),
    uriOut in {
  registryLookup!(`rho:rchain:revVault`, *revVaultCh) |
  registryLookup!(`rho:lang:listOps`, *listOpsCh) |

  for(@(_, RevVault) <- revVaultCh;
      @(_, ListOps) <- listOpsCh) {
    new posRevAddressCh in {

      getCurrentUserAddress!(*posRevAddressCh) |
      for(@posRevAddress <- posRevAddressCh) {
        new stateCh, posVaultCh in {

          @RevVault!("findOrCreate", posRevAddress, *posVaultCh) |
          for (@(true, _) <- posVaultCh ) {

            // State structure:
            // pendingRewards : Map[PublickKey, Int] - are accummulated by calling "pay"
            // committedRewards : Map[PublickKey, Int] - are moved from pendingRewards at each closeBlock
            // allbonds : Map[PublickKey, Int] - each validator stake
            stateCh!(({}, {}, $$initialBonds$$)) |

            contract PoS (@"getBonds", returnCh) = {
              new tmpCh in {
                getMVar!(*stateCh, *tmpCh) |
                for (@(_, _, allBonds) <- tmpCh) {
                  returnCh!(allBonds)
                }
              }
            } |

            /**
             * Returns a Map[PublidKey, Int] containing the rewards accumulated for each validator.
             * The returned map contains only the committed rewards after the last closeBlock
             */
            contract PoS (@"getRewards", returnCh) = {
              new tmpCh in {
                getMVar!(*stateCh, *tmpCh) |
                for (@(_, rewards, _) <- tmpCh) {
                  returnCh!(rewards)
                }
              }
            } |

            contract PoS (@"bond", @amount, returnCh) = {
              new userCh, depositCh, processCh in {
                runMVar!(*stateCh, *processCh, *returnCh) |

                getUser!(*userCh) |
                for(@(pending, rewards, allBonds), resultCh <- processCh;
                    @userPk <- userCh) {
                  if (allBonds.contains(userPk)) {
                    resultCh!((pending, rewards, allBonds), (false, "Public key is already bonded."))
                  } else if (amount < $$minimumBond$$) {
                    resultCh!((pending, rewards, allBonds), (false, "Bond is less than minimum!"))
                  } else if (amount > $$maximumBond$$) {
                    resultCh!((pending, rewards, allBonds), (false, "Bond is greater than maximum!"))
                  } else {
                    deposit!(userPk, amount, *depositCh) |
                    for(@depositResult <- depositCh) {
                      match depositResult {
                        (true, _) => {
                          resultCh!(
                            (pending, rewards, allBonds.set(userPk,amount)),
                            depositResult
                          )
                        }

                        (false, errorMsg) => {
                          resultCh!((pending, rewards, allBonds), (false, "Bond deposit failed: " ++ errorMsg))
                        }
                      }
                    }
                  }
                }
              }
            } |

            contract PoS (@"pay", @amount, returnCh) = {
              new vaultCh, transferCh, userCh, processPayCh,
                  depositCh,
                  newPendingCh in {
                getUser!(*userCh) |
                runMVar!(*stateCh, *processPayCh, *returnCh) |
                for(@(pending, rewards, allBonds), payResultCh <- processPayCh;
                    @userPk <- userCh) {
                  deposit!(userPk, amount, *depositCh) |

                  distributeRewards!(amount, allBonds, pending, *newPendingCh) |

                  for(@depositResult <- depositCh;
                      @newPending <- newPendingCh) {
                    payResultCh!((newPending, rewards, allBonds), depositResult)
                  }
                }
              }
            } |

            contract PoS(@"slash", @blockHash, returnCh) = {
              new userCh, invalidBlocksCh, doSlashCh,
                  getInvalidBlocks(`rho:casper:invalidBlocks`) in {
                getInvalidBlocks!(*invalidBlocksCh) |
                getUser!(*userCh) |

                runMVar!(*stateCh, *doSlashCh, *returnCh) |
                for (@invalidBlocks <- invalidBlocksCh;
                     @userPk <- userCh;
                     @(pending, rewards, allBonds), slashResultCh <- doSlashCh) {
                  new toBeSlashed in {
                    toBeSlashed!(invalidBlocks.getOrElse(blockHash, userPk)) |
                    for (@validator <- toBeSlashed) {
                      // TODO: Transfer to coop wallet instead of just simply setting bonds to 0
                      slashResultCh!(
                        (pending.set(validator, 0),
                         rewards.set(validator, 0),
                         allBonds.set(validator, 0)),
                        true)
                    }
                  }
                }
              }
            } |

            contract PoS(@"closeBlock", ackCh) = {
              new blockDataCh, mvarProcessCh,
                  getBlockData(`rho:block:data`),
                  commitReward, rewardsCh in {
                getBlockData!(*blockDataCh) |

                runMVar!(*stateCh, *mvarProcessCh, *ackCh) |
                for(_, @blockNumber <- blockDataCh;
                    @(pendingRewards, rewards, allBonds), mvarResultCh <- mvarProcessCh) {

                  if (blockNumber % 1000 == 0) {
                    new stdout(`rho:io:stdout`) in {
                      stdout!("TODO: a validator change should happen")
                    }
                  } |


                  @ListOps!("fold", pendingRewards.toList(), rewards, *commitReward, *rewardsCh) |
                  contract commitReward(@(pk, pending), @acc, resultCh) = {
                    resultCh!(acc.set(pk, acc.getOrElse(pk, 0) + pending))
                  } |

                  for (@rewards <- rewardsCh) {
                    mvarResultCh!(({}, rewards, allBonds), Nil)
                  }
                }
              }
            }
          }
        } |

        contract deposit (@fromUserPk, @amount, returnCh) = {
          new vaultCh,
              revAddressCh,
              authKeyCh in {
            @RevVault!("deployerAuthKey", *authKeyCh) |
            for (@authKey <- authKeyCh) {
              revAddressOps!("fromPublicKey", fromUserPk, *revAddressCh) |
              for (@fromRevAddress <- revAddressCh) {
                @RevVault!("findOrCreate", fromRevAddress, *vaultCh) |
                for (@(true, fromVault) <- vaultCh) {
                  @fromVault!("transfer", posRevAddress, amount, authKey, *returnCh)
                }
              }
            }
          }
        } |

        contract distributeRewards(@amount, @bonds, @originalRewards, returnCh) = {
          new computeSum, totalStakeCh,
              computeDelta, rewardsDeltaCh,
              computeMergeMap in {
            @ListOps!("fold", bonds.toList(), 0, *computeSum, *totalStakeCh) |
            contract computeSum(@(pk, stake), @acc, resultCh) = {
              resultCh!(acc + stake)
            } |

            for(@totalStake <- totalStakeCh) {
              @ListOps!("fold", bonds.toList(), {}, *computeDelta, *rewardsDeltaCh) |
              contract computeDelta(@(pk, currentValidatorStake), @acc, resultCh) = {
                resultCh!(acc.set(pk, (amount * currentValidatorStake) / totalStake))
              }
            } |

            for (@rewardsDelta <- rewardsDeltaCh) {

              @ListOps!("fold", rewardsDelta.toList(), originalRewards, *computeMergeMap, *returnCh) |
              contract computeMergeMap(@(pk, rewardDelta), @acc, resultCh) = {
                resultCh!(acc.set(pk, acc.getOrElse(pk, 0) + rewardDelta))
              }
            }
          }
        }
      } |

      contract getMVar(varCh, returnCh) = {
        for (@v <- varCh) {
          varCh!(v) |
          returnCh!(v)
        }
      } |
      contract runMVar(varCh, processCh, returnCh) = {
        new resultCh in {
          for (@v <- varCh) {
            processCh!(v, *resultCh) |
            for (@newV, @result <- resultCh) {
              varCh!(newV) |
              returnCh!(result)
            }
          }
        }
      } |

      contract getUser (returnCh) = {
        new parametersCh, getParameters(`rho:deploy:params`) in {
          getParameters!(*parametersCh) |
          for (_, _, @userPk, _ <- parametersCh) {
            returnCh!(userPk)
          }
        }
      } |

      contract getCurrentUserAddress(returnCh) = {
        new userCh in {
          getUser!(*userCh) |
          for (@userPk <- userCh) {
            revAddressOps!("fromPublicKey", userPk, *returnCh)
          }
        }
      } |

      contract getCurrentUserVault(returnCh) = {
        new revAddressCh in {
          getCurrentUserAddress!(*revAddressCh) |
          for (@revAddress <- revAddressCh) {
            @RevVault!("findOrCreate", revAddress, *returnCh)
          }
        }
      }
    } |
    rs!("047b43d6548b72813b89ac1b9f9ca67624a8b372feedd71d4e2da036384a3e1236812227e524e6f237cde5f80dbb921cac12e6500791e9a9ed1254a745a816fe1f".hexToBytes(),
    (9223372036854775807, bundle+{*PoS}),
    "3044022054ff4bae3984252b116e41e28d98bb5533eaa39aec2729228159166e2784f641022066a0fd99e7ea33df812fab095cbe61250f9548bce6da3ec4c6a90c741b94087f".hexToBytes(),
    *uriOut)
  }
}
